//------------------------------------------------------------------------------
//  shaders.shd
//  Shaders for RenderToCubemap sample
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
//  Lighting helper functions.
//
@code_block lighting

vec3 light(vec3 baseColor, vec3 eyeVec, vec3 normal, vec3 lightVec) {

    // ambient intensity
    float ambient = 0.25;

    // diffuse
    float n_dot_l = max(dot(normal, lightVec), 0.0);
    float diff = n_dot_l + ambient;

    // specular
    float specPower = 16.0;
    vec3 r = reflect(-lightVec, normal);
    float r_dot_v = max(dot(r, eyeVec), 0.0);
    float spec = pow(r_dot_v, specPower) * n_dot_l;

    return baseColor * (diff+ambient) + vec3(spec,spec,spec);
}

vec3 gamma(vec3 c) {
    float p = 1.0/2.2;
    return pow(c, vec3(p,p,p));
}
@end


//------------------------------------------------------------------------------
//  Render simple colored shapes
//
@uniform_block shapeVSParams VSParams
mat4 mvp ModelViewProjection
mat4 model Model
vec4 shapeColor Color
vec3 lightDir LightDir
vec3 eyePos EyePos
@end

@vs shapeVS
@use_uniform_block shapeVSParams
@in vec4 position
@in vec3 normal
@out vec3 worldPosition
@out vec3 worldNormal
@out vec3 worldEyePos
@out vec3 worldLightDir
@out vec4 color
    gl_Position = shapeVSParams.mvp * position;
    worldPosition = vec4(shapeVSParams.model * position).xyz;
    worldNormal = vec4(shapeVSParams.model * vec4(normal, 0.0)).xyz;
    worldEyePos = shapeVSParams.eyePos;
    worldLightDir = shapeVSParams.lightDir;
    color = shapeVSParams.shapeColor;
@end

@fs shapeFS
@use_code_block lighting
@in vec3 worldPosition
@in vec3 worldNormal
@in vec3 worldEyePos
@in vec3 worldLightDir
@in vec4 color
    vec3 eyeVec = normalize(worldEyePos - worldPosition);
    vec3 nrm = normalize(worldNormal);
    vec3 lightDir = normalize(worldLightDir);
    gl_FragColor = vec4(light(color.xyz, eyeVec, nrm, lightDir), 1.0);
@end

@fs shapeFSGamma
@use_code_block lighting
@in vec3 worldPosition
@in vec3 worldNormal
@in vec3 worldEyePos
@in vec3 worldLightDir
@in vec4 color
    vec3 eyeVec = normalize(worldEyePos - worldPosition);
    gl_FragColor = vec4(gamma(light(color.xyz, eyeVec, worldNormal, worldLightDir)), 1.0);
@end

@program ShapeShader shapeVS shapeFS
@program ShapeShaderWithGamma shapeVS shapeFSGamma

//------------------------------------------------------------------------------
//  Render shape with reflection/refraction.
//
@code_block util
// Adapted from http://developer.download.nvidia.com/SDK/9.5/Samples/DEMOS/Direct3D9/src/HLSL_FresnelReflection/docs/FresnelReflection.pdf
float fresnel(vec3 V, vec3 N, float R0)
{
    float cosAngle = 1.0-max(dot(V, N), 0.0);
    float result = cosAngle * cosAngle;
    result = result * result;
    result = result * cosAngle;
    result = clamp(result * (1.0 - R0) + R0, 0.0, 1.0);
    return result;
}
@end

@uniform_block sphereVSParams VSParams
mat4 mvp ModelViewProjection
mat4 model Model
vec3 lightDir LightDir
vec3 eyePos EyePos
@end

@texture_block textures Textures
samplerCube tex Texture
@end

@vs sphereVS
@use_uniform_block sphereVSParams
@in vec4 position
@in vec3 normal
@out vec3 worldPosition
@out vec3 worldNormal
@out vec3 worldEyePos
@out vec3 worldLightDir
    gl_Position = sphereVSParams.mvp * position;
    worldPosition = vec4(sphereVSParams.model * position).xyz;
    worldNormal = vec4(sphereVSParams.model * vec4(normal, 0.0)).xyz;
    worldEyePos = sphereVSParams.eyePos;
    worldLightDir = sphereVSParams.lightDir;
@end

@fs sphereFS
@use_texture_block textures
@use_code_block lighting
@use_code_block util
@in vec3 worldPosition
@in vec3 worldNormal
@in vec3 worldEyePos
@in vec3 worldLightDir
    vec3 eyeVec = normalize(worldEyePos.xyz - worldPosition.xyz);
    vec3 nrm = normalize(worldNormal.xyz);

    // reflection, refraction and fresnel
    vec3 refl = reflect(-eyeVec, nrm);
    vec3 refr = refract(-eyeVec, nrm, 0.7);
    vec4 reflectColor = texture(tex, refl);
    vec4 refractColor;
    if (length(refr) > 0.0) {
        refractColor = texture(tex, refr);
    }
    else {
        refractColor = reflectColor;
    }
    float fr = fresnel(eyeVec, nrm, 0.4);
    vec3 c = vec3(1.5, 1.2, 1.2) * fr * mix(refractColor.xyz, reflectColor.xyz, fr);
    c = light(c, eyeVec, nrm, normalize(worldLightDir));
    gl_FragColor = vec4(gamma(c), 1.0);
@end

@program SphereShader sphereVS sphereFS


